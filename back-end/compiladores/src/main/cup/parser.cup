package com.backend.compiladores.services;

import java_cup.runtime.*;
import com.backend.compiladores.services.traductor.*;
import com.backend.compiladores.services.parserPackage.*;
import java.util.Iterator;
import java.util.LinkedList;
import java.util.List;


class Parser;

//VIDEOS USADOS DE REFERENCIA

/*
https://youtu.be/R6znBEekUVA
https://youtu.be/4XKelO44u5U
https://youtu.be/4Z6Tnit810Y

AST (Abastact Syntax Tree)
https://youtu.be/kwG1ZOXUS4A

*/

//TAMBIEN RECOMIENDO LEER LA DOCUMENTACION DE JFLEX Y CUP, ES LARGA PERO VALE LA PENA LEERLA

/*RESULT = *** DEVUELVE LA CADENA QUE HICO MATCH CON EL ANALISIS SINTACTICO, SI SOLICITAS EL MATCH
DE UN NO TERMINAL, AGARRA EL RESULT DE LAS DERIVACIONES DE ESE NO TERMINAL, EN CASO DE NO ENCONTRAR DERIVACION
CON UNA RESPUESTA RESULT= *** REGRESA UN OBJETO NULL
*/


parser code
{:
//HACER CASO OMISO A ESTOS ERRORES SI ESTAS USANDO INTELLIJ
//Reescritura de los metodos nativos de CUP para mi conveniencia
    public Symbol s;
    public LinkedList<String> p_list = new LinkedList();

    //Asigna el token de error para despues poder sacarlo con el get
    public void syntax_error(Symbol s){
        this.s = s;
        this.report_expected_token_ids();
    }

    //Sacamos el token que da error
    public Symbol getSymbolError(){
        return this.s;

    }


    //CUP nos da una lista de los posibles token que se esperaban
    protected void report_expected_token_ids() {
        List<Integer> ids = this.expected_token_ids();
        LinkedList<String> list = new LinkedList();
        Iterator var3 = ids.iterator();

        while(var3.hasNext()) {
            Integer expected = (Integer)var3.next();
            list.add(this.symbl_name_from_id(expected));
        }

        this.p_list=list;
    }

    //Sacamos esa lista
    public LinkedList<String> getExpectedTokens(){
            return this.p_list;
    }

    //-----------------------------------------------------------

    public Ast ast = new Ast();
    public long contador = 1;

    /*ARRAY OF OBJECT TO STRING = ats*/
    private String ats(Object[] array){
        String answer = "";

        for(int i = 0; i < array.length; i++) {
          answer+= array[i].toString();
        }

        return answer;
    }

:};


non terminal
/*Inicio*/
inicio,
/*Instrucciones*/
instruccion, lista_instrucciones,
/*Expresiones*/
expr,
/*Valores*/
valor,
/*Declaraciones*/
declaracion,lista_variables,lista_valores,
/*Comentario*/
comentario,
/*Asignacion*/
asignar,
/*condicion*/
condicion, op_relacional, op_logico
;
terminal
/*Bloque principal o de inicio*/
INICIO,FIN,
/*Expresiones*/
SUMA,RESTA,MULTI,DIVI,POTENCIA,MODULO,LPAREN,RPAREN,
/*Valores*/
NUMERO, CADENA, BOOLEAN, CARACTER,
/*Declaracion*/
INGRESAR, VARIABLE, COMO, TIPO, CON_VALOR, COMA,
/*Comentarios*/
COMENTARIO,
/*Asignacion*/
ASIGNACION,
/*Operadores relacionales y logicos*/
MAYOR, MENOR, MAYORIGUAL,MENORIGUAL,IGUAL,DIFERENTE,OR,AND,NOT
;

/*Precedencia de operadores para expresiones*/
precedence left SUMA, RESTA;
precedence left MULTI, DIVI, MODULO;
precedence left POTENCIA;

/*Precedencia de operadores relacionales y logicos*/
precedence left MAYOR , MENOR , MAYORIGUAL , MENORIGUAL , IGUAL , DIFERENTE;
precedence left OR,AND;


start with inicio;

inicio ::= INICIO:e1 lista_instrucciones:e2 FIN:e3
        {:
            Nodo tmp = new Nodo("Global");
            tmp.setNumNodo(contador++);
            tmp.addHijo(new Nodo("Nodo inicio",e1.toString()));
            tmp.addHijo((Nodo) e2);
            tmp.addHijo(new Nodo("Nodo final", e3.toString()));
            ast.raiz = tmp;
            RESULT=tmp;
        :}
        /*|  COMENTARIO:e1 inicio
        {:
            Nodo tmp = new Nodo("Inicio");
            tmp.setNumNodo(contador++);
            tmp.addHijo(new Nodo(e1));
            tmp.addHijo(new Nodo(e2));
            tmp.addHijo((new Nodo(e3));
            ast.raiz.addHijo(new Nodo(tmp));
            RESULT=tmp;
        :}*/;


lista_instrucciones ::=  instruccion:e1 lista_instrucciones:e2
                     {:
                        Nodo tmp = new Nodo("instruccion");
                        tmp.setNumNodo(contador++);
                        tmp.addHijo((Nodo)e1);
                        tmp.addHijo((Nodo)e2);
                        RESULT=tmp;
                     :}
                     | /*Vacio*/;

instruccion ::= declaracion:e1
            {:
                Nodo tmp = new Nodo("declaracion");
                tmp.setNumNodo(contador++);
                tmp.addHijo((Nodo)e1);
                RESULT=tmp;
            :}
            | asignar:e1
            | COMENTARIO:e1
            {:
                Nodo tmp = new Nodo("comentario",e1.toString());
                tmp.setNumNodo(contador++);
                RESULT=tmp;
            :};

/*Valores*/
valor ::= CADENA:e1 {:
                    Nodo tmp = new Nodo("Cadena",e1.toString());
                    tmp.setNumNodo(contador++);
                    RESULT=tmp;
                     :}
        | BOOLEAN:e2{:
                    Nodo tmp = new Nodo("Boolean",e2.toString());
                    tmp.setNumNodo(contador++);
                    RESULT=tmp;
                    :}
        | CARACTER:e3{:
                    Nodo tmp = new Nodo("Caracter",e3.toString());
                    tmp.setNumNodo(contador++);
                    RESULT=tmp;
         :}
        | expr:e4{:
                    Nodo tmp = new Nodo("expresion");
                    tmp.setNumNodo(contador++);
                    tmp.addHijo((Nodo)e4);
                    RESULT=tmp;
        :};

/*Expresiones*/
expr ::= expr:e1 SUMA:e2 expr:e3 {:
            Nodo tmp = new Nodo("suma");
            tmp.setNumNodo(contador++);
            tmp.addHijo((Nodo) e1) /*no terminal*/;
            tmp.addHijo(new Nodo("suma",e2.toString())) /*terminal*/;
            tmp.addHijo((Nodo) e3) /*no terminal*/;
            RESULT=tmp;
        :}
        |expr:e1 RESTA:e2 expr:e3 {:
            Nodo tmp = new Nodo("resta");
            tmp.setNumNodo(contador++);
            tmp.addHijo((Nodo) e1) /*no terminal*/;
            tmp.addHijo(new Nodo("resta",e2.toString())) /*terminal*/;
            tmp.addHijo((Nodo) e3) /*no terminal*/;
            RESULT=tmp;
        :}
        |expr:e1 MULTI:e2 expr:e3 {:
            Nodo tmp = new Nodo("multi");
            tmp.setNumNodo(contador++);
            tmp.addHijo((Nodo) e1) /*no terminal*/;
            tmp.addHijo(new Nodo("multi",e2.toString())) /*terminal*/;
            tmp.addHijo((Nodo) e3) /*no terminal*/;
            RESULT=tmp;
        :}
        |expr:e1 DIVI:e2 expr:e3 {:
            Nodo tmp = new Nodo("divi");
            tmp.setNumNodo(contador++);
            tmp.addHijo((Nodo) e1) /*no terminal*/;
            tmp.addHijo(new Nodo("divi",e2.toString())) /*terminal*/;
            tmp.addHijo((Nodo) e3) /*no terminal*/;
            RESULT=tmp;
        :}
        |expr:e1 MODULO:e2 expr:e3 {:
            Nodo tmp = new Nodo("modulo");
            tmp.setNumNodo(contador++);
            tmp.addHijo((Nodo) e1) /*no terminal*/;
            tmp.addHijo(new Nodo("modulo",e2.toString())) /*terminal*/;
            tmp.addHijo((Nodo) e3) /*no terminal*/;
            RESULT=tmp;
        :}
        |expr:e1 POTENCIA:e2 expr:e3 {:
            Nodo tmp = new Nodo("potencia");
            tmp.setNumNodo(contador++);
            tmp.addHijo((Nodo) e1) /*no terminal*/;
            tmp.addHijo(new Nodo("potencia",e2.toString())) /*terminal*/;
            tmp.addHijo((Nodo) e3) /*no terminal*/;
            RESULT=tmp;
        :}
        |LPAREN:e1 expr:e2 RPAREN:e3 {:
            Nodo tmp = new Nodo("parentesis");
            tmp.setNumNodo(contador++);
            tmp.addHijo(new Nodo("LParen",e1.toString())) /*terminal*/;
            tmp.addHijo((Nodo) e2) /*no terminal*/;
            tmp.addHijo(new Nodo("Rparen",e3.toString())) /*terminal*/;
            RESULT=tmp;
        :}
        |RESTA:e1 NUMERO:e2 {:
            Nodo tmp = new Nodo("negativo",(e1.toString()+e2.toString()));
            tmp.setNumNodo(contador++);
            RESULT=tmp;

        :}
        |NUMERO:e1 {:
            Nodo tmp = new Nodo("numero",e1.toString());
            tmp.setNumNodo(contador++);
            RESULT=tmp;
         :};

/*Declaracion*/
lista_variables ::= VARIABLE:e1 COMA:e2 lista_variables:e3 {:
                    Nodo tmp = new Nodo("variables");
                    tmp.setNumNodo(contador++);
                    tmp.addHijo(new Nodo("Variable",e1.toString())) /*terminal*/;
                    tmp.addHijo(new Nodo("Coma",e2.toString())) /*terminal*/;
                    tmp.addHijo((Nodo) e3) /*no terminal*/;
                    RESULT=tmp;
                :}
                | VARIABLE:e1 {:
                    Nodo tmp = new Nodo("variable",e1.toString());
                    tmp.setNumNodo(contador++);
                    RESULT=tmp;
                 :};

lista_valores ::= valor:e1 COMA:e2 lista_valores:e3 {:
                    Nodo tmp = new Nodo("valores");
                    tmp.setNumNodo(contador++);
                    tmp.addHijo((Nodo) e1) /*no terminal*/;
                    tmp.addHijo(new Nodo("COMA",e2.toString())) /*terminal*/;
                    tmp.addHijo((Nodo) e3) /*no terminal*/;
                    RESULT=tmp;
                :}
                | valor:e1 {:
                    Nodo tmp = new Nodo("valores");
                    tmp.setNumNodo(contador++);
                    tmp.addHijo((Nodo)e1);
                    RESULT=tmp;
                 :};

declaracion ::= INGRESAR:e1 lista_variables:e2 COMO:e3 TIPO:e4 CON_VALOR:e5 lista_valores:e6
{:
    Nodo tmp = new Nodo("declaracion");
    tmp.setNumNodo(contador++);
    tmp.addHijo(new Nodo("Ingresar",e1.toString())) /*terminal*/;
    tmp.addHijo((Nodo) e2) /*no terminal*/;
    tmp.addHijo(new Nodo("COMO",e3.toString())) /*terminal*/;
    tmp.addHijo(new Nodo("TIPO",e4.toString())) /*terminal*/;
    tmp.addHijo(new Nodo("CON_VALOR",e5.toString())) /*terminal*/;
    tmp.addHijo((Nodo) e6) /*no terminal*/;
    RESULT=tmp;
 :};

/*Asignacion*/
asignar ::= lista_variables ASIGNACION expr;

/*Condiciones*/

op_relacional ::= MAYOR | MENOR | MAYORIGUAL | MENORIGUAL | IGUAL | DIFERENTE;
op_logico ::= OR|AND;

condicion ::= expr op_relacional expr
            | condicion op_logico condicion
            | NOT condicion;

